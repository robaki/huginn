
#hide.
#show design_type/1.
#show design_activity_det/1.
#show add/1.
#show remove/1.
compartment(c_05).
gene(g1,none).
protein(p1,none).
metabolite(met1,none).
metabolite(met2,none).
complex(cplx1,none).
growth(growth).
reaction(r1).
reaction(r2).
substrate(met2,none,c_05,growth).
substrate(met1,none,c_05,r1).
product(met2,none,c_05,r1).
substrate(p1,none,c_05,r2).
product(cplx1,none,c_05,r2).
model(m0;m1).
added_to_model(setup_present(met1,none,c_05),m0).
added_to_model(setup_present(p1,none,c_05),m0).
added_to_model(r1,m0).
added_to_model(growth,m0).
added_to_model(setup_present(met1,none,c_05),m1).
added_to_model(setup_present(p1,none,c_05),m1).
added_to_model(growth,m1).
added_to_model(r2,m1).
nr(0,m0).
probability(1, m0).
nr(1,m1).
probability(1, m1).
0{remove(setup_present(p1, none, c_05)),add(setup_present(met1, none, c_01)),design_type(basic_reconstruction_exp),design_entity_det(cplx1),design_compartment(c_10),design_type(enz_reconstruction_exp),design_entity_det(met2),design_compartment(c_07),design_compartment(c_03),design_entity_loc(cplx1),design_activity_det(growth),design_compartment(c_21),design_compartment(c_33),design_entity_loc(p1),design_compartment(c_22),design_entity_det(p1),design_compartment(c_16),design_compartment(c_25),add(setup_present(met2, none, c_01)),design_compartment(c_23),design_compartment(c_29),design_type(adam_two_factor_exp),design_compartment(c_14),design_compartment(c_31),design_compartment(c_27),design_available(met1),design_type(detection_activity_exp),design_compartment(c_09),design_activity_rec(r2),design_compartment(c_34),design_compartment(c_30),design_compartment(c_11),design_compartment(c_12),design_available(cplx1),design_type(detection_entity_exp),design_compartment(c_04),design_entity_det(met1),design_compartment(c_28),design_compartment(c_24),design_type(transp_reconstruction_exp),design_compartment(c_19),design_available(met2),remove(setup_present(met1, none, c_05)),design_compartment(c_17),design_compartment(c_15),design_compartment(c_26),design_compartment(c_08),design_compartment(c_05),design_compartment(c_20),design_activity_rec(r1),design_compartment(c_18),design_available(p1),design_compartment(c_02),design_type(localisation_entity_exp),design_compartment(c_32),design_compartment(c_06),design_compartment(c_13)}57.

designed :- designed(experiment(adam_two_factor_exp, Gene, Metabolite)), gene(Gene, Ver), metabolite(Metabolite, Ver).
designed :- designed(experiment(transp_reconstruction_exp, Activity, Entity)), activity(Activity), entity(Entity, Ver).
designed :- designed(experiment(enz_reconstruction_exp, Activity, Entity)), activity(Activity), entity(Entity, Ver).
designed :- designed(experiment(basic_reconstruction_exp, Activity)), activity(Activity).
designed :- designed(experiment(detection_activity_exp, Activity)), activity(Activity).
designed :- designed(experiment(localisation_entity_exp, Entity, Compartment)), entity(Entity, Ver), compartment(Compartment).
designed :- designed(experiment(detection_entity_exp, Entity)), entity(Entity, Ver).

:- not designed.

:- designed(Experiment1), designed(Experiment2), Experiment1 != Experiment2.

has_true_model :- predicts(Model, Experiment, true), model(Model), designed(Experiment).
has_false_model :- predicts(Model, Experiment, false), model(Model), designed(Experiment).
:- not has_true_model.
:- not has_false_model.

:- designed(experiment(adam_two_factor_exp, Gene, Metabolite)), add(Whatever).
:- designed(experiment(adam_two_factor_exp, Gene, Metabolite)), remove(Whatever).
:- involved(Entity, Version1, Model), involved(Entity, Version2, Model), Version1 != Version2.
:- designed(experiment(detection_entity_exp, met1)).

#const n = 10.

exp_score(0,Prb,0,0) :- indifferent(Model, Experiment), designed(Experiment), model(Model), nr(0,Model), probability(Prb, Model).
exp_score(0,0,Prb,0) :- predicts(Model, Experiment, true), designed(Experiment), model(Model), nr(0,Model), probability(Prb, Model).
exp_score(0,0,0,Prb) :- predicts(Model, Experiment, false), designed(Experiment), model(Model), nr(0,Model), probability(Prb, Model).

exp_score(I,Ind+Prb,Tr,Fa) :- exp_score(I-1, Ind, Tr, Fa), indifferent(Model, Experiment), designed(Experiment), model(Model), nr(I, Model), probability(Prb, Model).
exp_score(I,Ind,Tr+Prb,Fa) :- exp_score(I-1, Ind, Tr, Fa), predicts(Model, Experiment, true), designed(Experiment), model(Model), nr(I, Model), probability(Prb, Model).
exp_score(I,Ind,Tr,Fa+Prb) :- exp_score(I-1, Ind, Tr, Fa), predicts(Model, Experiment, false), designed(Experiment), model(Model), nr(I, Model), probability(Prb, Model).

exp_score(I) :- exp_score(I,Ind,Tr,Fa).

final_score(I,Ind,Tr,Fa) :- exp_score(I,Ind,Tr,Fa), not exp_score(I+1).

final_score(|Tr*10-n| + |Fa*10-n| + Ind*10) :- final_score(I,Ind,Tr,Fa).
#minimize[final_score(Score) = Score@2].

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%% rules for exp design %%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

designed(experiment(adam_two_factor_exp, Gene, Metabolite)) :-
	design_type(adam_two_factor_exp),
	design_deletable(Gene),
	design_available(Metabolite).

designed(experiment(transp_reconstruction_exp, Activity, Entity)) :-
	design_type(transp_reconstruction_exp),
	design_activity_rec(Activity),
	design_available(Entity).

designed(experiment(enz_reconstruction_exp, Activity, Entity)) :-
	design_type(enz_reconstruction_exp),
	design_activity_rec(Activity),
	design_available(Entity).

designed(experiment(basic_reconstruction_exp, Activity)) :-
	design_type(basic_reconstruction_exp),
	design_activity_rec(Activity).

designed(experiment(detection_activity_exp, Activity)) :-
	design_type(detection_activity_exp),
	design_activity_det(Activity).

designed(experiment(localisation_entity_exp, Entity, Compartment)) :-
	design_type(localisation_entity_exp),
	design_entity_loc(Entity),
	design_compartment(Compartment).

designed(experiment(detection_entity_exp, Entity)) :-
	design_type(detection_entity_exp),
	design_entity_det(Entity).%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%% application of interventions to all models %%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

removed(ActivityOrCondition, Model) :-
	remove(ActivityOrCondition),
	model(Model).

added_to_model(ActivityOrCondition, Model) :-
	add(ActivityOrCondition),
	model(Model).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% prediction rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%

growth(dummy).

indifferent(Model, Experiment) :-
	not predicts(Model, Experiment, true),
	not predicts(Model, Experiment, false),
	model(Model),
	designed(Experiment).

% Adams reconstruction
predicts(Model, experiment(adam_two_factor_exp, Gene, Metabolite), false) :-
	predicts(Model, experiment(detection_activity_exp, GrowthActivity), true),
	growth(GrowthActivity),
	not predicts(Model, experiment(adam_two_factor_exp, Gene, Metabolite), true),
	involved(Gene, GeneVer, Model),
	gene(Gene, GeneVer),
	involved(Metabolite, MetVersion, Model),
	metabolite(Metabolite, MetVersion).

predicts(Model, experiment(adam_two_factor_exp, Gene, Metabolite), true) :-% for genes that produce enzymes
	predicts(Model, experiment(detection_activity_exp, GrowthActivity), true),
	growth(GrowthActivity),
	substrate(Gene, GeneVer, GeneComp, ExpressionAct),
	compartment(GeneComp),
	expression(ExpressionAct),
	in_model(ExpressionAct, Model),
	metabolite(Metabolite, MetVersion),
	% expression produces a catalyst
	product(Ent, Ver, Comp, ExpressionAct),
	catalyses(Ent, Ver, Reaction),
	in_model(Reaction, Model),
	enz_required(Reaction),% redundancy, but better make sure (might also help the ASP solver)
	enz_compartment(SomeComp, Reaction),
	connected_compartments(Comp, SomeComp),
	% metabolite is within accepted distance from the catalysed reaction
	distance(Reaction, Metabolite, MetVersion, MetComp, Integer, Model),
	compartment(MetComp).

predicts(Model, experiment(adam_two_factor_exp, Gene, Metabolite), true) :-% for genes that produce subunits of enzymes
	predicts(Model, experiment(detection_activity_exp, GrowthActivity), true),
	growth(GrowthActivity),
	substrate(Gene, GeneVer, GeneComp, ExpressionAct),
	compartment(GeneComp),
	expression(ExpressionAct),
	in_model(ExpressionAct, Model),
	metabolite(Metabolite, MetVersion),
	% expression produces sth that is involved in production of enzyme down the line
	product(EntTo, VerTo, CompTo, ExpressionAct),
	entity(EntFrom, VerFrom),
	compartment(CompFrom),
	path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int),
	int(Integer),
	catalyses(EntFrom, VerFrom, Reaction),
	% enzyme is in appropriate compartment
	enz_compartment(SomeComp, Reaction),
	connected_compartments(SomeComp, CompFrom),
	compartment(SomeComp),
	% metabolite is within accepted distance from the catalysed reaction
	distance(Reaction, Metabolite, MetVersion, MetComp, Integer, Model),
	compartment(MetComp).

int(0;1;2).% distance goes up to 2

distance(ActivityFrom, EntTo, VerTo, CompTo, 0, Model) :-
	product(EntTo, VerTo, CompTo, ActivityFrom),
	in_model(ActivityFrom, Model),
	reaction(ActivityFrom),% only metabolic reactions
	enz_required(ActivityFrom).% optimisation - only these activities make sense in the context of experiment

distance(ActivityFrom, EntTo, VerTo, CompTo, Int+1, Model) :-
	distance(ActivityFrom, InterEnt, InterVer, InterComp, Int, Model),
	substrate(InterEnt, InterVer, InterComp, InterActivity),
	product(EntTo, VerTo, CompTo, InterActivity),
	in_model(InterActivity, Model),
	reaction(InterActivity),% only metabolic reactions
	Int < 2,
	int(Int).


% in vitro reconstruction: transporter required
predicts(Model, experiment(transp_reconstruction_exp, Activity, Entity), true) :-
	in_model(Activity, Model),
	transp_required(Activity),
	involved(Entity, Version, Model),
	transports(Entity, Version, Activity).

predicts(Model, experiment(transp_reconstruction_exp, Activity, Entity), false) :-
	in_model(Activity, Model),
	transp_required(Activity),
	involved(Entity, Version, Model),% there is a version of the entity in the model
	not transports(Entity, Version, Activity),% that doesnt transport that stuff
	transports(Entity, OtherVersion, Activity).% but some propose that this entity could do that


% in vitro reconstruction: enzymatic
predicts(Model, experiment(enz_reconstruction_exp, Activity, Entity), true) :-
	in_model(Activity, Model),
	enz_required(Activity),
	involved(Entity, Version, Model),
	catalyses(Entity, Version, Activity).

predicts(Model, experiment(enz_reconstruction_exp, Activity, Entity), false) :-
	in_model(Activity, Model),
	enz_required(Activity),
	involved(Entity, Version, Model),% there is a version of the entity in the model
	not catalyses(Entity, Version, Activity),% that doesnt catalyse the reaction
	catalyses(Entity, OtherVersion, Activity).% but some propose that this entity could do that


% in vitro reconstruction: basic
predicts(Model, experiment(basic_reconstruction_exp, Activity), true) :-
	activity(Activity),
	in_model(Activity, Model),
	not enz_required(Activity),
	not transp_required(Activity).


% localisation experiments: detecting entities
predicts(Model, experiment(localisation_entity_exp, Entity, Compartment), true) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	compartment(Compartment),
	involved(Entity, Version, Model),
	synthesizable(Entity, Version, Compartment, Model).

predicts(Model, experiment(localisation_entity_exp, Entity, Compartment), true) :-
	compartment(Compartment),
	in_model(setup_present(Entity, Version, Compartment), Model).

predicts(Model, experiment(localisation_entity_exp, Entity, Compartment), false) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	compartment(Compartment),
	involved(Entity, Version, Model),
	not predicts(Model, experiment(localisation_entity_exp, Entity, Compartment), true).


% detection experiments: detecting entities
predicts(Model, experiment(detection_entity_exp, Entity), true) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	compartment(Compartment),
	involved(Entity, Version, Model),
	synthesizable(Entity, Version, Compartment, Model).

predicts(Model, experiment(detection_entity_exp, Entity), true) :-% for stuff in the setup
	compartment(Compartment),
	in_model(setup_present(Entity, Version, Compartment), Model).

predicts(Model, experiment(detection_entity_exp, Entity), false) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	involved(Entity, Version, Model),
	not predicts(Model, experiment(detection_entity_exp, Entity), true).


% growth experiments (or any other detecting activities)
predicts(Model, experiment(detection_activity_exp, Activity), true) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	in_model(Activity, Model),
	activity(Activity),
	active(Activity, Model).

predicts(Model, experiment(detection_activity_exp, Activity), false) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	in_model(Activity, Model),
	activity(Activity),
	not active(Activity, Model).

predicts(Model, experiment(detection_activity_exp, GrowthActivity), false) :-
	growth(GrowthActivity),
	in_model(growth, Model),
	activity(growth),
	not active(growth, Model).

% involved species (to handle open world)
involved(Entity, Version, Model) :-
	compartment(Compartment),
	initially_present(Entity, Version, Compartment, Model).

involved(Entity, Version, Model) :-
	compartment(Compartment),
	product(Entity, Version, Compartment, Activity),
	in_model(Activity, Model).

involved(Entity, Version, Model) :-
	compartment(Compartment),
	substrate(Entity, Version, Compartment, Activity),
	in_model(Activity, Model).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% model specification rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
activity(Activity) :- reaction(Activity).
activity(Activity) :- transport(Activity).
activity(Activity) :- expression(Activity).
activity(Activity) :- complex_formation(Activity).
activity(Activity) :- growth(Activity).

entity(Entity, Version) :- metabolite(Entity, Version).
entity(Entity, Version) :- complex(Entity, Version).
entity(Entity, Version) :- protein(Entity, Version).
entity(Entity, Version) :- gene(Entity, Version).

connected_compartments(Compartment2,Compartment1) :- connected_compartments(Compartment1,Compartment2).
connected_compartments(Compartment,Compartment) :- compartment(Compartment).

in_model(ActivityOrSetup, Model) :-
	added_to_model(ActivityOrSetup, Model),
	not removed(ActivityOrSetup, Model).

initially_present(Entity, Version, Compartment, Model) :-
	in_model(setup_present(Entity, Version, Compartment), Model).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% synthesizable Entity rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
synthesizable(Ent, Version, Compartment, Model) :-
	activity(Activity),
	product(Ent, Version, Compartment, Activity),
	active(Activity, Model).

active(Activity, Model) :-
	activity(Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model).

eliminated(Activity, Model) :-
	eliminated(Activity, Model, Int),
	iteration(Int).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% activities elimination rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% iterations counter
iteration(0).
iteration(1).

iteration(Int+2) :-
	Int < 2,
	iteration(Int),
	eliminated(Activity, Model, Int+1),
	not eliminated(Activity, Model, Int),
	in_model(Activity, Model),
	activity(Activity),
	model(Model).

% reachability of one specie from another going only from products to substrates (i.e. back).
path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, 0) :-
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(EntTo, VerTo, CompTo, Activity),
	in_model(Activity, Model),
	activity(Activity).

path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, 0) :-
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(InterEnt, InterVer, InterComp, Activity),
	in_model(Activity, Model),
	path_back_from_to(InterEnt, InterVer, InterComp, EntTo, VerTo, CompTo, Model, 0).

% reachability for subsequent iterations.
path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int+1) :-
	iteration(Int),
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(EntTo, VerTo, CompTo, Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model, Int), % not eliminated on previous iteration
	activity(Activity).

path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int+1) :-
	iteration(Int),
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(InterEnt, InterVer, InterComp, Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model, Int), % not eliminated on previous iteration
	path_back_from_to(InterEnt, InterVer, InterComp, EntTo, VerTo, CompTo, Model, Int+1).

% is initially_present entity reachable from particular entity
path_back_to_initially_present(EntFrom, VerFrom, CompFrom, Model, Int) :-
	iteration(Int),
	path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int),
	initially_present(EntTo, VerTo, CompTo, Model).

path_back_to_initially_present(Ent, Ver, Comp, Model, Int) :-
	iteration(Int),
	initially_present(Ent, Ver, Comp, Model).

% has enzymes/transporter
has_enzyme(Activity, Model, Int) :-
	iteration(Int),
	in_model(Activity, Model),
	enz_required(Activity),
	compartment(Comp),
	enz_compartment(Comp, Activity),
	catalyses(Entity, Version, Activity),
	connected_compartments(SomeComp, Comp),
	path_back_to_initially_present(Entity, Version, SomeComp, Model, Int).

has_transporter(Activity, Model, Int) :-
	iteration(Int),
	in_model(Activity, Model),
	transp_required(Activity),
	compartment(Comp),
	transp_compartment(Comp, Activity),
	transports(Entity, Version, Activity),
	path_back_to_initially_present(Entity, Version, Comp, Model, Int).

% elimination of activities
eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	substrate(EntFrom, VerFrom, CompFrom, Activity),
	not path_back_to_initially_present(EntFrom, VerFrom, CompFrom, Model, Int).

eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	enz_required(Activity),
	not has_enzyme(Activity, Model, Int).

eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	transp_required(Activity),
	not has_transporter(Activity, Model, Int).