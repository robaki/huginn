
metabolite(met1,none).
metabolite(met2,none).
compartment(c_01).
result(r1, experiment(detection_entity_exp, met1), true).
model(m_0).
added_to_model(setup_present(met1,none,c_01),m_0).
added_to_model(setup_present(met2,none,c_01),m_0).
relevant(r1, m_0).
:- inconsistent(m_0, r1).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% model specification rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
activity(Activity) :- reaction(Activity).
activity(Activity) :- transport(Activity).
activity(Activity) :- expression(Activity).
activity(Activity) :- complex_formation(Activity).
activity(Activity) :- growth(Activity).

entity(Entity, Version) :- metabolite(Entity, Version).
entity(Entity, Version) :- complex(Entity, Version).
entity(Entity, Version) :- protein(Entity, Version).
entity(Entity, Version) :- gene(Entity, Version).

connected_compartments(Compartment2,Compartment1) :- connected_compartments(Compartment1,Compartment2).
connected_compartments(Compartment,Compartment) :- compartment(Compartment).

in_model(ActivityOrSetup, Model) :-
	added_to_model(ActivityOrSetup, Model),
	not removed(ActivityOrSetup, Model).

initially_present(Entity, Version, Compartment, Model) :-
	in_model(setup_present(Entity, Version, Compartment), Model).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% synthesizable Entity rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
synthesizable(Ent, Version, Compartment, Model) :-
	activity(Activity),
	product(Ent, Version, Compartment, Activity),
	active(Activity, Model).

active(Activity, Model) :-
	activity(Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model).

eliminated(Activity, Model) :-
	eliminated(Activity, Model, Int),
	iteration(Int).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% activities elimination rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% iterations counter
iteration(0).
iteration(1).

iteration(Int+2) :-
	Int < 4,
	iteration(Int),
	eliminated(Activity, Model, Int+1),
	not eliminated(Activity, Model, Int),
	in_model(Activity, Model),
	activity(Activity),
	model(Model).

% reachability of one specie from another going only from products to substrates (i.e. back).
path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, 0) :-
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(EntTo, VerTo, CompTo, Activity),
	in_model(Activity, Model),
	activity(Activity).

path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, 0) :-
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(InterEnt, InterVer, InterComp, Activity),
	in_model(Activity, Model),
	path_back_from_to(InterEnt, InterVer, InterComp, EntTo, VerTo, CompTo, Model, 0).

% reachability for subsequent iterations.
path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int+1) :-
	iteration(Int),
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(EntTo, VerTo, CompTo, Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model, Int), % not eliminated on previous iteration
	activity(Activity).

path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int+1) :-
	iteration(Int),
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(InterEnt, InterVer, InterComp, Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model, Int), % not eliminated on previous iteration
	path_back_from_to(InterEnt, InterVer, InterComp, EntTo, VerTo, CompTo, Model, Int+1).

% is initially_present entity reachable from particular entity
path_back_to_initially_present(EntFrom, VerFrom, CompFrom, Model, Int) :-
	iteration(Int),
	path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int),
	initially_present(EntTo, VerTo, CompTo, Model).

path_back_to_initially_present(Ent, Ver, Comp, Model, Int) :-
	iteration(Int),
	initially_present(Ent, Ver, Comp, Model).

% has enzymes/transporter
has_enzyme(Activity, Model, Int) :-
	iteration(Int),
	in_model(Activity, Model),
	enz_required(Activity),
	compartment(Comp),
	enz_compartment(Comp, Activity),
	catalyses(Entity, Version, Activity),
	connected_compartments(SomeComp, Comp),
	path_back_to_initially_present(Entity, Version, SomeComp, Model, Int).

has_transporter(Activity, Model, Int) :-
	iteration(Int),
	in_model(Activity, Model),
	transp_required(Activity),
	compartment(Comp),
	transp_compartment(Comp, Activity),
	transports(Entity, Version, Activity),
	path_back_to_initially_present(Entity, Version, Comp, Model, Int).

% elimination of activities
eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	substrate(EntFrom, VerFrom, CompFrom, Activity),
	not path_back_to_initially_present(EntFrom, VerFrom, CompFrom, Model, Int).

eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	enz_required(Activity),
	not has_enzyme(Activity, Model, Int).

eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	transp_required(Activity),
	not has_transporter(Activity, Model, Int).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% prediction rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%

growth(dummy).

indifferent(Model, Experiment) :-
	not predicts(Model, Experiment, true),
	not predicts(Model, Experiment, false),
	model(Model),
	designed(Experiment).

% Adams reconstruction
predicts(Model, experiment(adam_two_factor_exp, Gene, Metabolite), false) :-
	predicts(Model, experiment(detection_activity_exp, GrowthActivity), true),
	growth(GrowthActivity),
	not predicts(Model, experiment(adam_two_factor_exp, Gene, Metabolite), true),
	involved(Gene, GeneVer, Model),
	gene(Gene, GeneVer),
	involved(Metabolite, MetVersion, Model),
	metabolite(Metabolite, MetVersion).

predicts(Model, experiment(adam_two_factor_exp, Gene, Metabolite), true) :-% for genes that produce enzymes
	predicts(Model, experiment(detection_activity_exp, GrowthActivity), true),
	growth(GrowthActivity),
	substrate(Gene, GeneVer, GeneComp, ExpressionAct),
	compartment(GeneComp),
	expression(ExpressionAct),
	in_model(ExpressionAct, Model),
	metabolite(Metabolite, MetVersion),
	% expression produces a catalyst
	product(Ent, Ver, Comp, ExpressionAct),
	catalyses(Ent, Ver, Reaction),
	in_model(Reaction, Model),
	enz_required(Reaction),% redundancy, but better make sure (might also help the ASP solver)
	enz_compartment(SomeComp, Reaction),
	connected_compartments(Comp, SomeComp),
	% metabolite is within accepted distance from the catalysed reaction
	distance(Reaction, Metabolite, MetVersion, MetComp, Integer, Model),
	compartment(MetComp).

predicts(Model, experiment(adam_two_factor_exp, Gene, Metabolite), true) :-% for genes that produce subunits of enzymes
	predicts(Model, experiment(detection_activity_exp, GrowthActivity), true),
	growth(GrowthActivity),
	substrate(Gene, GeneVer, GeneComp, ExpressionAct),
	compartment(GeneComp),
	expression(ExpressionAct),
	in_model(ExpressionAct, Model),
	metabolite(Metabolite, MetVersion),
	% expression produces sth that is involved in production of enzyme down the line
	product(EntTo, VerTo, CompTo, ExpressionAct),
	entity(EntFrom, VerFrom),
	compartment(CompFrom),
	path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int),
	int(Integer),
	catalyses(EntFrom, VerFrom, Reaction),
	% enzyme is in appropriate compartment
	enz_compartment(SomeComp, Reaction),
	connected_compartments(SomeComp, CompFrom),
	compartment(SomeComp),
	% metabolite is within accepted distance from the catalysed reaction
	distance(Reaction, Metabolite, MetVersion, MetComp, Integer, Model),
	compartment(MetComp).

int(0;1;2).% distance goes up to 2

distance(ActivityFrom, EntTo, VerTo, CompTo, 0, Model) :-
	product(EntTo, VerTo, CompTo, ActivityFrom),
	in_model(ActivityFrom, Model),
	reaction(ActivityFrom),% only metabolic reactions
	enz_required(ActivityFrom).% optimisation - only these activities make sense in the context of experiment

distance(ActivityFrom, EntTo, VerTo, CompTo, Int+1, Model) :-
	distance(ActivityFrom, InterEnt, InterVer, InterComp, Int, Model),
	substrate(InterEnt, InterVer, InterComp, InterActivity),
	product(EntTo, VerTo, CompTo, InterActivity),
	in_model(InterActivity, Model),
	reaction(InterActivity),% only metabolic reactions
	Int < 2,
	int(Int).


% in vitro reconstruction: transporter required
predicts(Model, experiment(transp_reconstruction_exp, Activity, Entity), true) :-
	in_model(Activity, Model),
	transp_required(Activity),
	involved(Entity, Version, Model),
	transports(Entity, Version, Activity).

predicts(Model, experiment(transp_reconstruction_exp, Activity, Entity), false) :-
	in_model(Activity, Model),
	transp_required(Activity),
	involved(Entity, Version, Model),% there is a version of the entity in the model
	not transports(Entity, Version, Activity),% that doesnt transport that stuff
	transports(Entity, OtherVersion, Activity).% but some propose that this entity could do that


% in vitro reconstruction: enzymatic
predicts(Model, experiment(enz_reconstruction_exp, Activity, Entity), true) :-
	in_model(Activity, Model),
	enz_required(Activity),
	involved(Entity, Version, Model),
	catalyses(Entity, Version, Activity).

predicts(Model, experiment(enz_reconstruction_exp, Activity, Entity), false) :-
	in_model(Activity, Model),
	enz_required(Activity),
	involved(Entity, Version, Model),% there is a version of the entity in the model
	not catalyses(Entity, Version, Activity),% that doesnt catalyse the reaction
	catalyses(Entity, OtherVersion, Activity).% but some propose that this entity could do that


% in vitro reconstruction: basic
predicts(Model, experiment(basic_reconstruction_exp, Activity), true) :-
	activity(Activity),
	in_model(Activity, Model),
	not enz_required(Activity),
	not transp_required(Activity).


% localisation experiments: detecting entities
predicts(Model, experiment(localisation_entity_exp, Entity, Compartment), true) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	compartment(Compartment),
	involved(Entity, Version, Model),
	synthesizable(Entity, Version, Compartment, Model).

predicts(Model, experiment(localisation_entity_exp, Entity, Compartment), true) :-
	compartment(Compartment),
	in_model(setup_present(Entity, Version, Compartment), Model).

predicts(Model, experiment(localisation_entity_exp, Entity, Compartment), false) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	compartment(Compartment),
	involved(Entity, Version, Model),
	not predicts(Model, experiment(localisation_entity_exp, Entity, Compartment), true).


% detection experiments: detecting entities
predicts(Model, experiment(detection_entity_exp, Entity), true) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	compartment(Compartment),
	involved(Entity, Version, Model),
	synthesizable(Entity, Version, Compartment, Model).

predicts(Model, experiment(detection_entity_exp, Entity), true) :-% for stuff in the setup
	compartment(Compartment),
	in_model(setup_present(Entity, Version, Compartment), Model).

predicts(Model, experiment(detection_entity_exp, Entity), false) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	involved(Entity, Version, Model),
	not predicts(Model, experiment(detection_entity_exp, Entity), true).


% growth experiments (or any other detecting activities)
predicts(Model, experiment(detection_activity_exp, Activity), true) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	in_model(Activity, Model),
	activity(Activity),
	active(Activity, Model).

predicts(Model, experiment(detection_activity_exp, Activity), false) :-
	not predicts(Model, experiment(detection_activity_exp, GrowthActivity), false),
	growth(GrowthActivity),
	in_model(Activity, Model),
	activity(Activity),
	not active(Activity, Model).

predicts(Model, experiment(detection_activity_exp, GrowthActivity), false) :-
	growth(GrowthActivity),
	in_model(growth, Model),
	activity(growth),
	not active(growth, Model).

% involved species (to handle open world)
involved(Entity, Version, Model) :-
	compartment(Compartment),
	initially_present(Entity, Version, Compartment, Model).

involved(Entity, Version, Model) :-
	compartment(Compartment),
	product(Entity, Version, Compartment, Activity),
	in_model(Activity, Model).

involved(Entity, Version, Model) :-
	compartment(Compartment),
	substrate(Entity, Version, Compartment, Activity),
	in_model(Activity, Model).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%% inconsistency between models and results - for revision %%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

not_clean_model(Model) :-
	activity(Activity),
	in_model(Activity, Model),
	not active(Activity, Model).

inconsistent(Model, Result) :-
	predicts(Model, Experiment, Prediction),
	result(Result, Experiment, Outcome),
	Prediction != Outcome,
	relevant(Result, Model),
	not ignored(Result).