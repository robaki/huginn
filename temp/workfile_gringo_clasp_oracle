
gene(g1,none).
protein(p1,none).
metabolite(met1,none).
metabolite(met2,none).
complex(cplx1,none).
compartment(c_05).
growth(growth).
reaction(r1).
reaction(r2).
substrate(met2,none,c_05,growth).
substrate(met1,none,c_05,r1).
product(met2,none,c_05,r1).
substrate(p1,none,c_05,r2).
product(cplx1,none,c_05,r2).
model(copied_m0).
added_to_model(setup_present(met1,none,c_05),copied_m0).
added_to_model(setup_present(p1,none,c_05),copied_m0).
added_to_model(r1,copied_m0).
added_to_model(growth,copied_m0).
#hide.
#show active/2.
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% model specification rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
activity(Activity) :- reaction(Activity).
activity(Activity) :- transport(Activity).
activity(Activity) :- expression(Activity).
activity(Activity) :- complex_formation(Activity).
activity(Activity) :- growth(Activity).

entity(Entity, Version) :- metabolite(Entity, Version).
entity(Entity, Version) :- complex(Entity, Version).
entity(Entity, Version) :- protein(Entity, Version).
entity(Entity, Version) :- gene(Entity, Version).

connected_compartments(Compartment2,Compartment1) :- connected_compartments(Compartment1,Compartment2).
connected_compartments(Compartment,Compartment) :- compartment(Compartment).

in_model(ActivityOrSetup, Model) :-
	added_to_model(ActivityOrSetup, Model),
	not removed(ActivityOrSetup, Model).

initially_present(Entity, Version, Compartment, Model) :-
	in_model(setup_present(Entity, Version, Compartment), Model).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% synthesizable Entity rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
synthesizable(Ent, Version, Compartment, Model) :-
	activity(Activity),
	product(Ent, Version, Compartment, Activity),
	active(Activity, Model).

active(Activity, Model) :-
	activity(Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model).

eliminated(Activity, Model) :-
	eliminated(Activity, Model, Int),
	iteration(Int).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% activities elimination rules %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% iterations counter
iteration(0).
iteration(1).

iteration(Int+2) :-
	Int < 2,
	iteration(Int),
	eliminated(Activity, Model, Int+1),
	not eliminated(Activity, Model, Int),
	in_model(Activity, Model),
	activity(Activity),
	model(Model).

% reachability of one specie from another going only from products to substrates (i.e. back).
path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, 0) :-
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(EntTo, VerTo, CompTo, Activity),
	in_model(Activity, Model),
	activity(Activity).

path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, 0) :-
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(InterEnt, InterVer, InterComp, Activity),
	in_model(Activity, Model),
	path_back_from_to(InterEnt, InterVer, InterComp, EntTo, VerTo, CompTo, Model, 0).

% reachability for subsequent iterations.
path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int+1) :-
	iteration(Int),
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(EntTo, VerTo, CompTo, Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model, Int), % not eliminated on previous iteration
	activity(Activity).

path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int+1) :-
	iteration(Int),
	product(EntFrom, VerFrom, CompFrom, Activity),
	substrate(InterEnt, InterVer, InterComp, Activity),
	in_model(Activity, Model),
	not eliminated(Activity, Model, Int), % not eliminated on previous iteration
	path_back_from_to(InterEnt, InterVer, InterComp, EntTo, VerTo, CompTo, Model, Int+1).

% is initially_present entity reachable from particular entity
path_back_to_initially_present(EntFrom, VerFrom, CompFrom, Model, Int) :-
	iteration(Int),
	path_back_from_to(EntFrom, VerFrom, CompFrom, EntTo, VerTo, CompTo, Model, Int),
	initially_present(EntTo, VerTo, CompTo, Model).

path_back_to_initially_present(Ent, Ver, Comp, Model, Int) :-
	iteration(Int),
	initially_present(Ent, Ver, Comp, Model).

% has enzymes/transporter
has_enzyme(Activity, Model, Int) :-
	iteration(Int),
	in_model(Activity, Model),
	enz_required(Activity),
	compartment(Comp),
	enz_compartment(Comp, Activity),
	catalyses(Entity, Version, Activity),
	connected_compartments(SomeComp, Comp),
	path_back_to_initially_present(Entity, Version, SomeComp, Model, Int).

has_transporter(Activity, Model, Int) :-
	iteration(Int),
	in_model(Activity, Model),
	transp_required(Activity),
	compartment(Comp),
	transp_compartment(Comp, Activity),
	transports(Entity, Version, Activity),
	path_back_to_initially_present(Entity, Version, Comp, Model, Int).

% elimination of activities
eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	substrate(EntFrom, VerFrom, CompFrom, Activity),
	not path_back_to_initially_present(EntFrom, VerFrom, CompFrom, Model, Int).

eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	enz_required(Activity),
	not has_enzyme(Activity, Model, Int).

eliminated(Activity, Model, Int) :-
	iteration(Int),
	activity(Activity),
	in_model(Activity, Model),
	transp_required(Activity),
	not has_transporter(Activity, Model, Int).